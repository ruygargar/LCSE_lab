
; Programa completo para su uso con el PIC

; ZONA DE CONSTANTES ASCII

I	:	X49 ; CODIGO ASCII PARA LA I
A 	:	X41 ; CODIGO ASCII PARA LA A
C 	:	X43 ; CODIGO ASCII PARA LA C
T 	:	X54 ; CODIGO ASCII PARA LA T
S 	:	X53 ; CODIGO ASCII PARA LA S
E	:	X45 ; CODIGO ASCII PARA LA E
R	:	X52 ; CODIGO ASCII PARA LA R
O	: 	X4F ; CODIGO ASCII PARA LA O
N	:	X4E ; CODIGO ASCII PARA LA N
K	:	X4B ; CODIGO ASCII PARA LA K
F	:	X46 ; CODIGO ASCII PARA LA F

; ZONA DE CONSTANTES PARA MEMORIA

RCBUF0	:	X00 ; DIRECCIONES BUFFER RECEPCION
RCBUF1	:	X01
RCBUF2	:	X02
NINST	:	X03

TXBUF0	:	X04 ; DIRECCIONES BUFFER TRANSMISION
TXBUF1	:	X05 

SWBASE	:	X10 ; BASE PARA LA ZONA DE SWITCHES

LEVBASE	:	X20 ; BASE PARA LA ZONA DE ACTUADORES
TSTAT	:	X31

GPMEM	:	X40 ; BASE DE LA MEMORIA GENERAL
TMP		:	X41 ; UN BYTE PARA CALCULOS

; COMIENZO DEL CODIGO, ESPERA ACTIVA POR 
; VIGILANCIA DE NINST

#INIT	LD	.A, [NINST]
		LD	.B, XFF
		CMPL
		JMPT	#INIT

; LLEGA UNA NUEVA INSTRUCCION POR LINEA
; SERIE. DECODIFICADOR

		LD	.ACC, X00
		WR	NINST
		LD	.A, [RCBUF0]
		LD	.B, A
		CMPE
		JMPT	#LEVER
		LD	.B, I
		CMPE
		JMPT	#SWITCH
		LD	.B, T
		CMPE
		JMPT	#TEMP
		LD	.B, S
		CMPE
		JMPT	#SND
		JMP	#ERR

; COMPROBACION DEL DECODIFICADOR

#OUT	LD	.ACC, O
		WR	TXBUF0
		LD	.ACC, K
		WR	TXBUF1
		SEND
		JMP 	#INIT

; RUTINA DE ATENCION A LOS SWITCHES

#SWITCH	LD	.A, [RCBUF1]
		ASCII2BIN
		LD	.INDEX, .ACC
		LD	.A, .ACC
		LD	.B, X07
		CMPG
		JMPT	#ERR	; deberia saltar a ERR
		LD	.A, [RCBUF2]
		ASCII2BIN
		LD	.A, .ACC
		LD	.B, X01
		CMPG
		JMPT	#ERR	; deberia saltar a ERR
		WRI	SWBASE
		JMP	#OUT

; RUTINA DE ATENCION A LOS ACTUADORES

#LEVER	LD	.A, [RCBUF1]
		ASCII2BIN
		LD	.A, .ACC
		LD	.INDEX, .ACC
		LD	.B, XFF
		CMPE
		JMPT	#ERR
		LD	.A, [RCBUF2]
		ASCII2BIN
		LD	.A, .ACC
		LD	.B, X09
		CMPG
		JMPT	#ERR
		WRI	LEVBASE
		JMP	#OUT

; RUTINA DE ATENCION AL TERMOSTATO

#TEMP	LD	.A, [RCBUF1]
		ASCII2BIN
		LD	.A, .ACC
		LD	.B, X02
		CMPG
		JMPT	#ERR
		LD	.B, X00
		ADD
		SHIFTL
		SHIFTL
		SHIFTL
		SHIFTL
		WR	TMP
		LD	.A, [RCBUF2]
		ASCII2BIN
		LD	.A, .ACC
		LD	.B, XFF
		CMPE
		JMPT	#ERR
		LD	.B, [TMP]
		ADD
		WR	TSTAT
		JMP	#OUT

; ENVIO DE INFORMACION

#SND	LD	.A, [RCBUF1]
		LD	.B, A
		CMPE
		JMPT	#LEVSND
		LD	.B, I
		CMPE
		JMPT	#SWSND
		LD	.B, T
		CMPE
		JMPT	#TSND
		JMP	#ERR

; ENVIANDO INFO DE UN ACTUADOR

#LEVSND	LD	.A, [RCBUF2]
		ASCII2BIN
		LD	.A, .ACC
		LD	.INDEX, .ACC
		LD	.B, X09
		CMPG
		JMPT	#ERR
		LDI	.A, [LEVBASE]
		BIN2ASCII
		WR	TXBUF1
		LD	.ACC, A
		WR	TXBUF0
		SEND
		JMP	#INIT

; ENVIANDO INFO DE UN SWITCH

#SWSND	LD	.A, [RCBUF2]
		ASCII2BIN
		LD	.A, .ACC
		LD	.INDEX, .ACC
		LD	.B, X07
		CMPG
		JMPT	#ERR
		LDI	.A, [SWBASE]
		BIN2ASCII
		WR	TXBUF1
		LD	.ACC, S
		WR	TXBUF0
		SEND
		JMP	#INIT

; ENVIANDO INFO DEL TERMOSTATO

#TSND	LD	.A, [TSTAT]
		LD	.B, B11110000
		AND
		SHIFTR
		SHIFTR
		SHIFTR
		SHIFTR
		LD	.A, .ACC
		BIN2ASCII
		WR	TXBUF0
		LD	.A, [TSTAT]
		LD	.B, B00001111
		AND
		LD	.A, .ACC
		BIN2ASCII
		WR	TXBUF1
		SEND
		JMP	#INIT

#ERR	LD	.ACC, E
		WR	TXBUF0
		LD	.ACC, R
		WR	TXBUF1
		SEND
		JMP	#INIT

